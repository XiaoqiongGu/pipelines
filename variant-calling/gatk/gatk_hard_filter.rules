assert 'references' in config
assert 'genome' in config['references']


localrules: gatk_selectvariants
rule gatk_selectvariants:
    input:
        vcf = '{prefix}.vcf.gz',
        vcfidx = '{prefix}.vcf.gz.tbi',
        ref = config["references"]["genome"],
    output:
        vcf = temp("{prefix}.{type,(snp|indel)}.vcf.gz")
    #benchmark:
    #    "{prefix}.{type,(snp|indel)}.vcf.gz.{rule}.benchmark.log"
    log:
        "{prefix}.{type,(snp|indel)}.vcf.gz.log"
    params:
        type = lambda wildcards: wildcards.type.upper()
    threads:
        1
    message:
        "Extracting {params.type} from {input.vcf}"
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper"
        " -R {input.ref} -V {input.vcf} -o {output.vcf}"
        " -T SelectVariants -nt {threads} -selectType {params.type} >& {log}"

        
localrules: gatk_hard_filter_snps
rule gatk_hard_filter_snps:
    input:
        vcf = '{prefix}.snp.vcf.gz',
        ref = config["references"]["genome"],
    output:
        vcf = '{prefix}.snp_hfilter.vcf.gz'
    #benchmark:
    #    '{prefix}.snp_hfilter.vcf.gz.{rule}.benchmark.log'
    log:
        '{prefix}.snp_hfilter.vcf.gz.log'
    params:
        filter_name = 'snp_default_hard_filter',
        filter_expr = 'QD < 2.0 || FS > 60.0 || MQ < 40.0 || MQRankSum < -12.5 || ReadPosRankSum < -8.0'
    threads:
        # previously set higher to work around UGE/Java/OS vmem problem in GIS
        1
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper"
        " -T VariantFiltration -R {input.ref} -V {input.vcf}"
        " -o {output.vcf} --filterExpression '{params.filter_expr}'"
        " --filterName {params.filter_name} >& {log}"

        
localrules: gatk_hard_filter_indels
rule gatk_hard_filter_indels:
    input:
        vcf = '{prefix}.indel.vcf.gz',
        ref = config["references"]["genome"],
    output:
        vcf = '{prefix}.indel_hfilter.vcf.gz'
    #benchmark:
    #    '{prefix}.indel_hfilter.vcf.gz.{rule}.benchmark.log'
    log:
        '{prefix}.indel_hfilter.vcf.gz.log'
    params:
        filter_name = 'indel_default_hard_filter',
        filter_expr = 'QD < 2.0 || FS > 200.0 || ReadPosRankSum < -20.0'
    threads:
        # previously set higher to work around UGE/Java/OS vmem problem in GIS
        1
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper"
        " -T VariantFiltration -R {input.ref} -V {input.vcf}"
        " -o {output.vcf} --filterExpression '{params.filter_expr}'"
        " --filterName {params.filter_name} >& {log}"

