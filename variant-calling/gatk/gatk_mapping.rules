"""GATK based postprocessing of BAM files. Indels can be realigned and base qualities
can be recalibrated.

Based on rules/mapping/gatk.rules from https://bitbucket.org/johanneskoester/snakemake-workflows/ (commit fa47806)
- original author Johannes KÃ¶ster (http://johanneskoester.bitbucket.org)
- original license: MIT

Regarding use of known sites see 'What should I use as known variants/sites for running tool X?'
https://software.broadinstitute.org/gatk/guide/article?id=1247

Regarding use of intervals see 'When should I use -L to pass in a list of intervals?'
http://gatkforums.broadinstitute.org/wdl/discussion/4133/when-should-i-use-l-to-pass-in-a-list-of-intervals
"""


assert 'references' in config
assert 'genome' in config['references']
assert 'dbsnp' in config['references']
assert 'seqtype' in config
assert config['seqtype'] in ['WGS', 'WES', 'targeted']


def _get_padding_arg():
    """
    See http://gatkforums.broadinstitute.org/dsde/discussion/5792/target-intervals-for-exome
    NOTE: duplicated in gatk_haplotype_caller.rules
    """
    if config['seqtype'] in ['targeted', 'WES']:
        ip = config.get('interval_padding')
        if ip:
            return "-ip {}".format(ip)
        else:
            return ""
    else:
        return ""

    
# this is the global bed file, if given
def _get_intervals_arg():
    intervals = config.get("intervals")
    if intervals:
        assert os.path.exists(intervals)
        return "-L {} {}".format(intervals, _get_padding_arg())
    else:
        return ""


rule gatk_recalibrate_info:
    # see also  What should I use as known variants/sites for running tool X?
    # https://software.broadinstitute.org/gatk/guide/article?id=1247
    input:
        "{prefix}.bam.bai",
        ref=config['references']['genome'],
        bam="{prefix}.bam"
    output:
        temp("{prefix}.bqsr.grp")
    log:
        "{prefix}.bqsr.grp.log"
    params:
        intervals_arg=_get_intervals_arg(),
        custom=config.get("params_gatk", "")
    threads:
        8
    message:
        "{} BaseRecalibrator".format("Skipping" if config['seqtype'] == 'targeted' else "Running")
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper"
        " -T BaseRecalibrator -R {input.ref}"
        " -nct {threads} {params.intervals_arg} {params.custom}"
        " -I {input.bam}"
        " -knownSites {config[references][dbsnp]} -knownSites {config[references][mills]}"
        " -o {output} >& {log}"


rule gatk_recalibrate_bam:
    input:
        ref=config['references']['genome'],
        bam="{prefix}.bam",
        grp="{prefix}.bqsr.grp"
    output:
        "{prefix}.bqsr.bam"
    log:
        "{prefix}.bqsr.bam.log"
    params:
        custom=config.get("params_gatk", "")
    # set to 8 in original files. maxes out at that value on aquila
    threads:
        8
    message:
        "Running PrintReads for Recal"
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper"
        " -T PrintReads -R {input.ref} {params.custom}"
        " -nct {threads}"
        " --disable_bam_indexing"
        " -I {input.bam} -BQSR {input.grp}"
        " -o {output} >& {log}"
