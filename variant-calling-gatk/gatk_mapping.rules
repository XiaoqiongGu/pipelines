"""GATK based postprocessing of BAM files. Indels can be realigned and base qualities
can be recalibrated.

Based on rules/mapping/gatk.rules from https://bitbucket.org/johanneskoester/snakemake-workflows/ (commit fa47806)
- original author Johannes KÃ¶ster (http://johanneskoester.bitbucket.org)
- original license: MIT
"""


assert 'references' in config
assert 'genome' in config['references']
assert 'dbsnp' in config['references']


rule gatk_realign_info:
    input:
        bam="{prefix}.bam",
        bai="{prefix}.bam.bai",
        ref=config['references']['genome'],
        refdict=config['references']['genome'].replace("fasta", "dict")
    output:
        temp("{prefix}.realn.intervals")
    params:
        custom=config.get("params_gatk", "")
    threads: 8
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper"
        " -T RealignerTargetCreator -R {input.ref}"
        " -nt {threads} {params.custom}"
        " -I {input.bam} -known {config[references][dbsnp]}"
        " -o {output}"


rule gatk_realign_bam:
    input:
        ref=config['references']['genome'],
        bam="{prefix}.bam",
        intervals="{prefix}.realn.intervals"
    output:
        "{prefix}.realn.bam"
    params:
        custom=config.get("params_gatk", "")
    # single threaded only, but ask for more due to java/scheduler problem
    threads: 4
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper"
        " -T IndelRealigner -R {input.ref} {params.custom}"
        " --disable_bam_indexing"
        " -I {input.bam} -targetIntervals {input.intervals}"
        " -o {output}"


rule gatk_recalibrate_info:
    input:
        "{prefix}.bam.bai",
        ref=config['references']['genome'],
        bam="{prefix}.bam"
    output:
        temp("{prefix}.recal.grp")
    params:
        custom=config.get("params_gatk", "")
    threads: 8
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper"
        "-T BaseRecalibrator -R {input.ref} {params.custom}"
        " -nct {threads}"
        " -I {input.bam} -knownSites {config[references][dbsnp]}"
        " -o {output}"


rule gatk_recalibrate_bam:
    input:
        ref=config['references']['genome'],
        bam="{prefix}.bam",
        grp="{prefix}.recal.grp"
    output:
        "{prefix}.recal.bam"
    params:
        custom=config.get("params_gatk", "")
    # set to 8 in original files. maxes out at that value on aquila
    threads: 8
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper"
        " -T PrintReads -R {input.ref} {params.custom}"
        " -nct {threads}"
        " --disable_bam_indexing"
        " -I {input.bam} -BQSR {input.grp}"
        " -o {output}"
