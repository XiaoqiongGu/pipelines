"""GATK based postprocessing of BAM files. Indels can be realigned and base qualities
can be recalibrated.

Based on rules/mapping/gatk.rules from https://bitbucket.org/johanneskoester/snakemake-workflows/ (commit fa47806)
- original author Johannes KÃ¶ster (http://johanneskoester.bitbucket.org)
- original license: MIT
"""


rule gatk_realign_info:
    input:
        bam="mapping/{prefix}.bam",
        bai="mapping/{prefix}.bam.bai",
        ref=config['references']['genome'],
        refdict=config['references']['genome'].replace("fasta", "dict")
    output:
        temp("mapping/{prefix}.realn.intervals")
    params:
        custom=config.get("params_gatk", "")
    log:
        "mapping/log/{prefix}.realn_info.log"
    threads: 8
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper -T RealignerTargetCreator -R {input.ref} {params.custom} "
        "-nt {threads} "
        "-I {input.bam} -known {config[references][dbsnp]} "
        "-o {output} >& {log}"


rule gatk_realign_bam:
    input:
        ref=config['references']['genome'],
        bam="mapping/{prefix}.bam",
        intervals="mapping/{prefix}.realn.intervals"
    output:
        "mapping/{prefix}.realn.bam"
    params:
        custom=config.get("params_gatk", "")
    log:
        "mapping/log/{prefix}.realn.log"
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper -T IndelRealigner -R {input.ref} {params.custom} "
        "--disable_bam_indexing "
        "-I {input.bam} -targetIntervals {input.intervals} "
        "-o {output} >& {log}"


rule gatk_recalibrate_info:
    input:
        "mapping/{prefix}.bam.bai",
        ref=config['references']['genome'],
        bam="mapping/{prefix}.bam"
    output:
        temp("mapping/{prefix}.recal.grp")
    params:
        custom=config.get("params_gatk", "")
    log:
        "mapping/log/{prefix}.recal_info.log"
    threads: 8
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper -T BaseRecalibrator -R {input.ref} {params.custom} "
        "-nct {threads} "
        "-I {input.bam} -knownSites {config[references][dbsnp]} "
        "-o {output} >& {log}"


rule gatk_recalibrate_bam:
    input:
        ref=config['references']['genome'],
        bam="mapping/{prefix}.bam",
        grp="mapping/{prefix}.recal.grp"
    output:
        "mapping/{prefix}.recal.bam"
    params:
        custom=config.get("params_gatk", "")
    log:
        "mapping/log/{prefix}.recal.log"
    threads: 8
    shell:
        "GATK_THREADS={threads} GATK_MEM=16g gatk_wrapper -T PrintReads -R {input.ref} {params.custom} "
        "-nct {threads} "
        "--disable_bam_indexing "
        "-I {input.bam} -BQSR {input.grp} "
        "-o {output} >& {log}"
