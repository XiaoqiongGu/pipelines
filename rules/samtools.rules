"""Some samtools rules taken from snakemake-workflows.git/bio/ngs/rules/mapping/samfiles.rules

Requires samtools

Original author: Johannes KÃ¶ster (http://johanneskoester.bitbucket.org)
Original license: MIT
"""

rule samtools_fasta_index:
    input:
        "{prefix}.{suffix}"
    output:
        "{prefix}.{suffix,(fasta|fa)}.fai"
    log:
        "{prefix}.{suffix,(fasta|fa)}.fai.log"
    shell:
        "samtools faidx {input} >& {log};"


rule map_rate:
    input:
        bam="{prefix}.idxstats.txt",
    output:
        "{prefix}.maprate.txt"
    log:      
        "{prefix}.maprate.txt.log"
    shell:
        "cat {input} | awk '{{a+=$3; u+=$4}} END {{printf \"%.3f%% reads map\\n\", a/(a+u)*100.0}}' > {output} 2> {log}"


rule bam_idxstats:
    input:
        bam="{prefix}.bam",
        bai="{prefix}.bam.bai"
    output:
        "{prefix}.idxstats.txt"
    log:
        "{prefix}.idxstats.txt.log"
    shell:
        "samtools idxstats {input.bam} > {output} 2> {log};"


rule bam_stats:
    input:
        bam="{prefix}.bam"
    output:
        stats="{prefix}.bamstats/stats.txt",
        plothtml="{prefix}.bamstats/stats_plot.html"
    log:
        "{prefix}.bamstats/stats.log",
    shell:
        "od=$(dirname {output.stats}); test -d $od || mkdir $od;"
        "{{ samtools stats {input.bam} > {output.stats};"
        "plot-bamstats -p $(echo {output.plothtml} | sed -e 's,.html,,') {output.stats}; }} >&{log}"
        

rule bam_index:
    input:
        "{prefix}.bam"
    output:
        "{prefix}.bam.bai"
    log:
        "{prefix}.bam.bai.log"
    shell:
        "samtools index {input} >& {log};"


