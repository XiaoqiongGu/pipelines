"""Some samtools rules taken from snakemake-workflows.git/bio/ngs/rules/mapping/samfiles.rules

Requires samtools

Original author: Johannes KÃ¶ster (http://johanneskoester.bitbucket.org)
Original license: MIT
"""

localrules: samtools_fasta_index
rule samtools_fasta_index:
    input:
        "{prefix}.{suffix}"
    output:
        "{prefix}.{suffix,(fasta|fa)}.fai"
    benchmark:
        "{prefix}/.{suffix,(fasta|fa)}.fai.samtools_fasta_index.benchmark.log"        
    log:
        "{prefix}.{suffix,(fasta|fa)}.fai.log"
    shell:
        "samtools faidx {input} >& {log};"


localrules: map_rate
rule map_rate:
    """better to use bamstats/stats.txt as input

    WARN : not using only properly paired reads but any paired read
    """
    input:
        bam="{prefix}.idxstats.txt",
    output:
        "{prefix}.maprate.txt"
    benchmark:
        "{prefix}.maprate.map_rate.benchmark.log"    
    log:      
        "{prefix}.maprate.txt.log"
    shell:
        "cat {input} | awk '{{a+=$3; u+=$4}} END {{printf \"%.3f%% reads map\\n\", a/(a+u)*100.0}}' > {output} 2> {log}"


localrules: bam_idxstats
rule bam_idxstats:
    """rarely needed. bam_stats much better"""
    input:
        bam="{prefix}.bam",
        bai="{prefix}.bam.bai"
    output:
        "{prefix}.idxstats.txt"
    benchmark:
        "{prefix}.idxstats.bam_idxstats.benchmark.log"
    log:
        "{prefix}.idxstats.txt.log"
    shell:
        "samtools idxstats {input.bam} > {output} 2> {log};"


# this can run forever and execution seems to be blocked in such cases
#localrules: bam_stats
rule bam_stats:
    input:
        bam="{prefix}.bam"
    output:
        stats="{prefix}.bamstats/stats.txt",
        plothtml="{prefix}.bamstats/stats_plot.html"
    benchmark:
        "{prefix}.bamstats.bam_stats.benchmark.log"
    log:
        "{prefix}.bamstats/stats.log",
    shell:
        "od=$(dirname {output.stats}); test -d $od || mkdir $od;"
        "{{ samtools stats {input.bam} > {output.stats};"
        "plot-bamstats -p $(echo {output.plothtml} | sed -e 's,.html,,') {output.stats}; }} >&{log}"
        

localrules: bam_index
rule bam_index:
    input:
        "{prefix}.bam"
    output:
        "{prefix}.bam.bai"
    benchmark:
        "{prefix}.bam.bai.bam_index.benchmark.log"
    log:
        "{prefix}.bam.bai.log"
    shell:
        "samtools index {input} >& {log};"


