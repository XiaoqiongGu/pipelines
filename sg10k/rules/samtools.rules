"""Some samtools rules taken from snakemake-workflows.git/bio/ngs/rules/mapping/samfiles.rules

Requires samtools

Original author: Johannes KÃ¶ster (http://johanneskoester.bitbucket.org)
Original license: MIT
"""

rule samtools_fasta_index:
    input:
        "{prefix}.{suffix}"
    output:
        "{prefix}.{suffix,(fasta|fa)}.fai"
    shell:
        "samtools faidx {input};"


rule map_rate:
    input:
        bam="{prefix}.idxstats.txt",
    output:
        "{prefix}.maprate.txt"
    shell:
        "cat {input} | awk '{{a+=$3; u+=$4}} END {{printf \"%.3f%% reads map\\n\", a/(a+u)*100.0}}' > {output}"


rule bam_idxstats:
    input:
        bam="{prefix}.bam",
        bai="{prefix}.bam.bai"
    output:
        "{prefix}.idxstats.txt"
    shell:
        "samtools idxstats {input.bam} > {output};"


rule bam_stats:
    input:
        bam="{prefix}.bam"
    output:
        stats="{prefix}.bamstats/stats.txt",
        plothtml="{prefix}.bamstats/stats_plot.html"
    shell:
        "od=$(dirname {output.stats}); test -d $od || mkdir $od;"
        "samtools stats {input.bam} > {output.stats};"
        "plot-bamstats -p $(echo {output.plothtml} | cut -f -1 -d '.') {output.stats};"
        

rule bam_index:
    input:
        "{prefix}.bam"
    output:
        "{prefix}.bam.bai"
    shell:
        "samtools index {input};"


